const std = @import("std");
const windows = std.os.windows;
const DWORD = windows.DWORD;
const WORD = windows.WORD;

pub const console = @import("console.zig");
const ffi = @import("ffi/ffi.zig");
pub const handle = @import("handle.zig");
pub const screen_buffer = @import("screen_buffer.zig");
pub const csbi = @import("csbi.zig");

// zig fmt: off

/// Characters written by the WriteFile or WriteConsole function or echoed by
/// the ReadFile or ReadConsole function are examined for ASCII control
/// sequences and the correct action is performed. Backspace, tab, bell,
/// carriage return, and line feed characters are processed.
pub const ENABLE_PROCESSED_INPUT: DWORD         = 0x0001;

/// The ReadFile or ReadConsole function returns only when a carriage return
/// character is read. If this mode is disabled, the functions return when one
/// or more characters are available.
pub const ENABLE_LINE_INPUT: DWORD              = 0x0002;

/// Characters read by the ReadFile or ReadConsole function are written to the
/// active screen buffer as they are read. This mode can be used only if the
/// `ENABLE_LINE_INPUT` mode is also enabled.
pub const ENABLE_ECHO_INPUT: DWORD              = 0x0004;

/// User interactions that change the size of the console screen buffer are
/// reported in the console's input buffer.
pub const ENABLE_WINDOW_INPUT: DWORD            = 0x0008;

/// If the mouse pointer is within the borders of the console window and the
/// window has the keyboard focus, mouse events generated by mouse movement and
/// button presses are placed in the input buffer. These events are discarded by
/// ReadFile or ReadConsole, even when this mode is enabled.
pub const ENABLE_MOUSE_INPUT: DWORD             = 0x0010;

/// When enabled, text entered in a console window will be inserted at the
/// current cursor location and all text following that location will not be
/// overwritten. When disabled, all following text will be overwritten.
///
/// To enable this mode, use ENABLE_INSERT_MODE | ENABLE_EXTENDED_FLAGS.
/// To disable this mode, use ENABLE_EXTENDED_FLAGS without this flag.
pub const ENABLE_INSERT_MODE: DWORD             = 0x0020;

/// This flag enables the user to use the mouse to select and edit text.
/// To enable this mode, use ENABLE_QUICK_EDIT_MODE | ENABLE_EXTENDED_FLAGS.
/// To disable this mode, use ENABLE_EXTENDED_FLAGS without this flag.
pub const ENABLE_QUICK_EDIT_MODE: DWORD         = 0x0040;

/// Required to enable or disable extended flags.
/// See `ENABLE_INSERT_MODE` and `ENABLE_QUICK_EDIT_MODE`.
pub const ENABLE_EXTENDED_FLAGS: DWORD          = 0x0080;

/// Though defined in WinCon.h, this flag is otherwise undocumented. Best guess
/// is that it is related to the "Let system position window" check box on the
/// property sheet of a CMD.EXE window.
pub const ENABLE_AUTO_POSITION: DWORD           = 0x0100;

/// Setting this flag directs the Virtual Terminal processing engine to convert
/// user input received by the console window into ANSI sequences that can be
/// retrieved through `ReadFile` or `ReadConsole` functions.
pub const ENABLE_VIRTUAL_TERMINAL_INPUT: DWORD  = 0x0200;

pub const CONSOLE_TEXTMODE_BUFFER: DWORD        = 0x0001;

/// When writing with `WriteFile` or `WriteConsole` or echoing with `ReadFile`
/// or `ReadConsole`, the cursor moves to the beginning of the next row when it
/// reaches the end of the current row.
pub const ENABLE_WRAP_AT_EOL_OUTPUT: DWORD      = 0x0002;

pub const FROM_LEFT_1ST_BUTTON_PRESSED: DWORD   = 0x0001;
pub const RIGHTMOST_BUTTON_PRESSED: DWORD       = 0x0002;
pub const FROM_LEFT_2ND_BUTTON_PRESSED: DWORD   = 0x0004;
pub const FROM_LEFT_3RD_BUTTON_PRESSED: DWORD   = 0x0008;
pub const FROM_LEFT_4TH_BUTTON_PRESSED: DWORD   = 0x0010;

pub const RIGHT_ALT_PRESSED: DWORD              = 0x0001;
pub const LEFT_ALT_PRESSED: DWORD               = 0x0002;
pub const RIGHT_CTRL_PRESSED: DWORD             = 0x0004;
pub const LEFT_CTRL_PRESSED: DWORD              = 0x0008;
pub const SHIFT_PRESSED: DWORD                  = 0x0010;
pub const NUMLOCK_ON: DWORD                     = 0x0020;
pub const SCROLLLOCK_ON: DWORD                  = 0x0040;
pub const CAPSLOCK_ON: DWORD                    = 0x0080;
pub const ENHANCED_KEY: DWORD                   = 0x0100;

pub const MOUSE_MOVED: DWORD                    = 0x0001;
pub const DOUBLE_CLICK: DWORD                   = 0x0002;
pub const MOUSE_WHEELED: DWORD                  = 0x0004;
pub const MOUSE_HWHEELED: DWORD                 = 0x0008;

pub const KEY_EVENT: WORD                       = 0x0001;
pub const MOUSE_EVENT: WORD                     = 0x0002;
pub const WINDOW_BUFFER_SIZE_EVENT: WORD        = 0x0004;
pub const MENU_EVENT: WORD                      = 0x0008;
pub const FOCUS_EVENT: WORD                     = 0x0010;

/// Text color contains blue.
pub const FOREGROUND_BLUE: WORD                 = 0x0001;

/// Text color contains green.
pub const FOREGROUND_GREEN: WORD                = 0x0002;

/// Text color contains red.
pub const FOREGROUND_RED: WORD                  = 0x0004;

/// Text color is intensified (bright).
pub const FOREGROUND_INTENSITY: WORD            = 0x0008;

/// Background color contains blue.
pub const BACKGROUND_BLUE: WORD                 = 0x0010;

/// Background color contains green.
pub const BACKGROUND_GREEN: WORD                = 0x0020;

/// Background color contains red.
pub const BACKGROUND_RED: WORD                  = 0x0040;

/// Background color is intensified (bright).
pub const BACKGROUND_INTENSITY: WORD            = 0x0080;

/// Reverse foreground and background attributes.
pub const COMMON_LVB_REVERSE_VIDEO: WORD        = 0x4000;

/// Underscore.
pub const COMMON_LVB_UNDERSCORE: WORD           = 0x8000;
// zig fmt: on

pub const ConsoleError = error{
    FailedToRetrieveMode,
    FailedToSetMode,
    FailedToRetrieveInfo,
    FailedToCreateHandle,
    FailedToCreateScreenBuffer,
    FailedToShowScreenBuffer,
    FailedToWriteToHandle,
    FailedToSetWindowInfo,
    FailedToReadInput,
    FailedToGetHandle,
    FailedToSetCursorPosition,
    FailedToSetCursorInfo,
    FailedToSetTextAttributes,
    Unsupported,
};

pub const ConsoleCursorInfo = struct {
    size: u32,
    visible: bool,

    pub fn fromRaw(info: ffi.CONSOLE_CURSOR_INFO) @This() {
        return @This(){
            .size = info.dwSize,
            .visible = if (info.bVisible == 1) true else false,
        };
    }

    pub fn toRaw(self: @This()) ffi.CONSOLE_CURSOR_INFO {
        return ffi.CONSOLE_CURSOR_INFO{
            .dwSize = self.size,
            .bVisible = if (self.visible) windows.TRUE else windows.FALSE,
        };
    }
};

/// Defines the size of the terminal screen buffer, this is the same as
/// calcualting `Coord.right - Coord.left` and `Coord.bottom - Coord.top`
pub const Size = struct {
    width: i16,
    height: i16,
};

/// Defines the coordinates of the upper left and lower right corners of a
/// the terminal screen buffer.
pub const WindowPosition = struct {
    left: i16,
    right: i16,
    bottom: i16,
    top: i16,

    pub fn toSmallRect(self: @This()) windows.SMALL_RECT {
        return windows.SMALL_RECT{
            .Top = self.top,
            .Bottom = self.bottom,
            .Left = self.left,
            .Right = self.right,
        };
    }

    pub fn fromSmallRect(rect: windows.SMALL_RECT) @This() {
        return @This(){
            .left = rect.Left,
            .right = rect.Right,
            .bottom = rect.Bottom,
            .top = rect.Top,
        };
    }
};

/// Defines the coordinates of a character cell in a console screen buffer.
/// The origin of the coordinate system (0,0) is at the top, left cell of the
/// buffer.
pub const Coord = struct {
    x: i16,
    y: i16,

    pub fn new(x: i16, y: i16) @This() {
        return @This(){ .x = x, .y = y };
    }

    pub fn fromRaw(coord: windows.COORD) @This() {
        return @This(){
            .x = coord.X,
            .y = coord.Y,
        };
    }

    pub fn toRaw(self: @This()) windows.COORD {
        return windows.COORD{
            .X = self.x,
            .Y = self.y,
        };
    }
};

/// Describes an input event in the console input buffer.
pub const InputRecord = union(enum) {
    /// The Event member contains a `KeyEventRecord` with information about a
    /// keyboard event.
    KeyEvent: KeyEventRecord,
    /// The Event member contains a `MouseEventRecord` with information about a
    /// mouse movement or button press event.
    MouseEvent: MouseEventRecord,
    /// The Event member contains a `WindowBufferSizeRecord` with information
    /// about the new size of the console screen buffer.
    WindowBufferSizeEvent: WindowBufferSizeRecord,
    /// The Event member contains a `FocusEventRecord`. These events are used
    /// internally and should be ignored.
    FocusEvent: FocusEventRecord,
    /// The Event member contains a `MenuEventRecord`. These events are used
    /// internally and should be ignored. [See](https://learn.microsoft.com/en-us/windows/console/input-record-str#members)
    MenuEvent: MenuEventRecord,

    pub fn fromRaw(input_record: ffi.INPUT_RECORD) @This() {
        return switch (input_record.EventType) {
            KEY_EVENT => InputRecord{
                .KeyEvent = KeyEventRecord.fromRaw(input_record.Event.KeyEvent),
            },
            MOUSE_EVENT => InputRecord{
                .MouseEvent = MouseEventRecord.fromRaw(
                    input_record.Event.MouseEvent,
                ),
            },
            WINDOW_BUFFER_SIZE_EVENT => InputRecord{
                .WindowBufferSizeEvent = WindowBufferSizeRecord.fromRaw(
                    input_record.Event.WindowBufferSizeEvent,
                ),
            },
            MENU_EVENT => InputRecord{
                .MenuEvent = MenuEventRecord.fromRaw(
                    input_record.Event.MenuEvent,
                ),
            },
            FOCUS_EVENT => InputRecord{
                .FocusEvent = FocusEventRecord.fromRaw(
                    input_record.Event.FocusEvent,
                ),
            },
            else => unreachable,
        };
    }
};

/// The Event member contains a `KeyEventRecord` with information about a
/// keyboard event.
pub const KeyEventRecord = struct {
    key_down: bool,
    repeat_count: u16,
    virtual_key_code: u16,
    virtual_scan_code: u16,
    u_char: u16,
    control_key_state: ControlKeyState,

    pub fn fromRaw(key_event: ffi.KEY_EVENT_RECORD) @This() {
        return @This(){
            .key_down = if (key_event.bKeyDown == 1) true else false,
            .repeat_count = key_event.wRepeatCount,
            .virtual_key_code = key_event.wVirtualKeyCode,
            .virtual_scan_code = key_event.wVirtualScanCode,
            .control_key_state = @bitCast(key_event.dwControlKeyState),
            .u_char = @as(u16, key_event.uChar.UnicodeChar),
        };
    }
};

/// The Event member contains a `MouseEventRecord` with information about a
/// mouse movement or button press event.
pub const MouseEventRecord = struct {
    mouse_position: Coord,
    button_state: MouseButtonState,
    control_key_state: ControlKeyState,
    event_flags: EventFlags,

    pub fn fromRaw(mouse_event: ffi.MOUSE_EVENT_RECORD) @This() {
        return @This(){
            .mouse_position = Coord.fromRaw(mouse_event.dwMousePosition),
            .button_state = @bitCast(mouse_event.dwButtonState),
            .control_key_state = @bitCast(mouse_event.dwControlKeyState),
            .event_flags = @bitCast(mouse_event.dwEventFlags),
        };
    }
};

/// The Event member contains a `WindowBufferSizeRecord` with information
/// about the new size of the console screen buffer.
pub const WindowBufferSizeRecord = struct {
    size: Coord,

    pub fn fromRaw(window_buffer_size: ffi.WINDOW_BUFFER_SIZE_RECORD) @This() {
        return @This(){
            .size = Coord.fromRaw(window_buffer_size.dwSize),
        };
    }
};

/// The Event member contains a `FocusEventRecord`. These events are used
/// internally and should be ignored.
pub const FocusEventRecord = struct {
    set_focus: bool,

    pub fn fromRaw(focus_event: ffi.FOCUS_EVENT_RECORD) @This() {
        return @This(){
            .set_focus = if (focus_event.bSetFocus == 1) true else false,
        };
    }
};

/// The Event member contains a `MenuEventRecord`. These events are used
/// internally and should be ignored. [See](https://learn.microsoft.com/en-us/windows/console/input-record-str#members)
pub const MenuEventRecord = struct {
    command_id: u32,

    pub fn fromRaw(menu_event: ffi.MENU_EVENT_RECORD) @This() {
        return @This(){
            .command_id = @as(u32, menu_event.dwCommandId),
        };
    }
};

/// Represents the state of modifier keys given an event.
pub const ControlKeyState = packed struct {
    right_alt: bool,
    left_alt: bool,
    right_ctrl: bool,
    left_ctrl: bool,
    shift: bool,
    numlock: bool,
    scroll_lock: bool,
    capslock: bool,
    enhanced_key: bool,
    _pad: u23 = 0,

    pub fn controlPressed(self: @This()) bool {
        return self.left_ctrl | self.right_ctrl;
    }

    pub fn altPressed(self: @This()) bool {
        return self.left_alt | self.right_alt;
    }
};

// TODO: find a way to represent mouse wheel events
/// Represents the state of mouse buttons during a mouse input event.
pub const MouseButtonState = packed struct {
    from_left_first_button: bool,
    rightmost_button: bool,
    from_left_second_button: bool,
    from_left_third_button: bool,
    from_left_fourth_button: bool,
    _pad: u27 = 0,

    pub fn releaseButton(self: @This()) bool {
        return @as(u32, @bitCast(self)) == 0;
    }

    pub fn leftButtonPressed(self: @This()) bool {
        return self.from_left_first_button;
    }

    pub fn rightButtonPressed(self: @This()) bool {
        return self.rightmost_button or
            self.from_left_third_button or
            self.from_left_fourth_button;
    }

    pub fn middleButtonPressed(self: @This()) bool {
        return self.from_left_second_button;
    }

    pub fn scrollDown(self: @This()) bool {
        return @as(i32, @bitCast(self)) < 0;
    }

    pub fn scrollUp(self: @This()) bool {
        return @as(i32, @bitCast(self)) > 0;
    }

    pub fn scrollLeft(self: @This()) bool {
        return @as(i32, @bitCast(self)) < 0;
    }

    pub fn scrollRight(self: @This()) bool {
        return @as(i32, @bitCast(self)) > 0;
    }
};

/// Represents which kind of mouse event happened.
pub const EventFlags = packed struct {
    mouse_move: bool,
    double_click: bool,
    mouse_scroll: bool,
    mouse_horizontal_scroll: bool,
    _pad: u28 = 0,

    pub fn pressOrRelease(self: @This()) bool {
        return @as(u32, @bitCast(self)) == 0;
    }
};
